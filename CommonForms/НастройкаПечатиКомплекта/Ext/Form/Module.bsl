#Область ОбработчикиСобытийФормы

&НаСервере
Процедура КомплектыВPDF_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	РежимРаботы = "ПечатьКомплекта";
	ПутьКФайлу = "C:\123.pdf";
	
КонецПроцедуры

&НаКлиенте
Процедура КомплектыВPDF_ПриОткрытииПосле(Отказ)
	
	УправлениеФормойРасширения(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КомплектыВPDF_РежимРаботыПриИзмененииПосле(Элемент)
	
	УправлениеФормойРасширения(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомплектыВPDF_СохранитьВПДФПосле(Команда)
	
	Если КомплектПечатныхФорм.НайтиСтроки(Новый Структура("Печатать", Истина)).Количество() > 0 Тогда
		
		ИменаМакетов = ПодготовитьНастройкиДляПечати();
		
		ПечатныеФормы = УправлениеПечатьюВызовСервера.СформироватьПечатныеФормыДляБыстройПечати(
			ТипОбъекта, ИменаМакетов, Объекты.ВыгрузитьЗначения(), Новый Структура("ФиксированныйКомплект", Истина));
			
		НовыйТабличныйДокумент = Новый ТабличныйДокумент;
		СобратьТабличныйДокумент(НовыйТабличныйДокумент, ПечатныеФормы.ТабличныеДокументы);
		НовыйТабличныйДокумент.АвтоМасштаб = Истина;
		НовыйТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
		НовыйТабличныйДокумент.Записать(ПутьКФайлу, ТипФайлаТабличногоДокумента.PDF);
		
		Закрыть();
		
	Иначе
		
		ОчиститьСообщения();
		ТекстСообщения = НСтр("ru = 'Выберите хотя бы одну печатную форму для печати.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "КомплектПечатныхФорм");
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СобратьТабличныйДокумент(НовыйТабличныйДокумент, ТабличныеДокументы)
	
	Для Каждого ТабДокумент Из ТабличныеДокументы Цикл
		НовыйТабличныйДокумент.Вывести(ТабДокумент.Значение);
		НовыйТабличныйДокумент.ВывестиВертикальныйРазделительСтраниц();
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормойРасширения(Форма)
	
	Элементы = Форма.Элементы;
	
	ЭтоПечатьВPDF = (Форма.РежимРаботы = "СохранениеВPDF");
	Элементы.ПутьКФайлу.Видимость = ЭтоПечатьВPDF;
	Элементы.Печать.Видимость = НЕ ЭтоПечатьВPDF;
	Элементы.КомплектПечатныхФормЭкземпляров.Видимость = НЕ ЭтоПечатьВPDF;
	
	Элементы.ФормаСохранитьВПДФ.Видимость = ЭтоПечатьВPDF;
	Элементы.ПечатьСразуНаПринтер.Видимость = НЕ ЭтоПечатьВPDF;
	Если ЭтоПечатьВPDF Тогда
		Элементы.ФормаСохранитьВПДФ.КнопкаПоУмолчанию = Истина;
	Иначе
		Элементы.Печать.КнопкаПоУмолчанию = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти